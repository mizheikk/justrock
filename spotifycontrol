#!/usr/bin/env osascript

on run argv
  if count of argv is equal to 0 then
    set msg to       "{\"commands\" : [\n"
    set msg to msg & "  {\"play\":\"\"},\n"
    set msg to msg & "  {\"pause\":\"\"},\n"
    set msg to msg & "  {\"next\":\"\"},\n"
    set msg to msg & "  {\"previous\":\"\"},\n"            
    set msg to msg & "  {\"volume\":\"int 0-100\"},\n"   
    set msg to msg & "  {\"info\":\"\"}\n"       
    set msg to msg & "]}\n"
    return msg
  end if
       
  set command to item 1 of argv
	using terms from application "Spotify"
	
	if command is equal to "play" then
	  if count of argv is equal to 1 then
	    tell application "Spotify" to play
	  else
	    set uri to item 2 of argv
	    tell application "Spotify" to play track uri
	  end if  
	else if command is equal to "pause" then
	  tell application "Spotify" to pause
	else if command is equal to "previous" then
	  tell application "Spotify" to previous track
	else if command is equal to "next" then
	  tell application "Spotify" to next track
	else if command is equal to "volume" then
	  set newVolume to item 2 of argv
	    tell application "Spotify" 
	      set sound volume to newVolume
	    end tell
	else if command is equal to "info" then
	  tell application "Spotify"
	    set _artist to artist of current track
	    set _album to album of current track
	    set _trackName to name of current track
	    set _trackNumber to track number of current track
	    set _diskNumber to disc number of current track
	    set _duration to duration of current track
	    set _playedCount to played count of current track
	    set _starred to starred of current track
	    set _popularity to popularity of current track
	    set _trackId to id of current track
	    set _trackUrl to spotify url of current track
	    
	    set _volume to sound volume
	    
	    #json
	    set info to        "{\n"
	    set info to info & "   \"artist\":\"" & _artist & "\",\n"
	    set info to info & "   \"album\":\"" & _album & "\",\n"
	    set info to info & "   \"trackName\":\"" & _trackName & "\",\n"
	    set info to info & "   \"trackId\":\"" & _trackId & "\",\n"
	    set info to info & "   \"trackUrl\":\"" & _trackUrl & "\",\n"	    	    
	    set info to info & "   \"trackNumber\":" & _trackNumber & ",\n"
	    set info to info & "   \"diskNumber\":" & _diskNumber & ",\n"
	    set info to info & "   \"duration\":" & _duration & ",\n"
	    set info to info & "   \"playedCount\":" & _playedCount & ",\n"	    
	    set info to info & "   \"starred\":" & _starred & ",\n"		    
	    set info to info & "   \"popularity\":" & _popularity & ",\n"
	    set info to info & "   \"volume\":" & _volume & "\n"	    
	    set info to info & "}\n"	       		
	  end tell
	  return info
	end if 
	
	end using terms from
end run

